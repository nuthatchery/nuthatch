/**
 * Parses a Java source file to an abstract syntax tree.
 *
 * @author  Martin Bravenboer <martin@cs.uu.nl>
 */
module parse-java
imports liblib strategoxt-xtc-tools tool-doc jtree-version

strategies

  main-parse-java =
    xtc-io-wrap(
      release-option(|"parse source") + symbol-option
    , parse-java-usage
    , parse-java-about
    , !["sglr", "implode-asfix", "Java-15.tbl"]
    , parse-java
    )

strategies

  parse-java =
    if get-release => "1.5" then
      xtc-sglr-no-heuristics(get-parse-table, get-start-symbol)
    else 
      xtc-sglr(get-parse-table, get-start-symbol)
    end
    ; xtc-implode-asfix

  xtc-sglr-no-heuristics(tbl, sort) =
    xtc-transform(!"sglr", !["-fi", "-fe", "-2A", "-p", <tbl; xtc-find> (), "-s", <sort> () | <pass-v-verbose> ()])

strategies

  symbol-option =
    ArgOption("-s" + "--start-symbol"
    , set-start-symbol
    , !HelpString("-s|--start-symbol s", "start parsing with symbol s [CompilationUnit]")
    )

  set-start-symbol =
    <set-config> ("start-symbol", <id>)

  get-start-symbol = 
    <get-config> "start-symbol" <+ !"CompilationUnit"

/**
 * Documentation
 */
strategies

  parse-java-usage =
    <tool-doc>
      [ Usage("parse-java [OPTIONS]")
      , Summary(
         "Parses a Java source file to an abstract syntax 
          tree in the ATerm format.")
      , OptionUsage()
      , AutoReportBugs()
      ]

  parse-java-about =
    <tool-doc>
      [ AutoProgram()
      , Author(Person("Martin Bravenboer", "martin@cs.uu.nl"))
      , GNU_LGPL("2002-2004", "Martin Bravenboer <martin@cs.uu.nl>")
      , Config([
          DefaultXTCRepository()
        , CurrentXTCRepository()
        ])
      ]

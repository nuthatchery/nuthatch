module core-lift-eblocks
imports liblib Java-EBlock

strategies
  
  io-core-lift-eblocks =
    io-wrap(core-lift-eblocks)

  core-lift-eblocks =
    bottomup(try(lift-to-block-level))

  lift-to-block-level =
    ?Block(<id>)
    ; map(lift-eblocks)
    ; !Block(<concat>)

  /**
   * @todo  Nested EBlocks?
   * @todo  Nested Blocks?
   * @todo  Is Block the only block level element?
   * @todo  Anonymous inner classes?
   * @todo  Is flatten-list required?
   */
  lift-eblocks =
      collect-split-om-pre-eblock
    ; ?(<id>, pre-stms)
    ; collect-split-om-post-eblock
    ; ?(t, post-stms)
    ; ( !post-stms => []; !pre-stms => []
      < ![t]
      + <concat> [<map(lift-eblocks); concat> pre-stms, <lift-eblocks> t, <map(lift-eblocks); concat> post-stms]
      )

  collect-split-om-pre-eblock =
    !(<alltd(?PreEBlock(_, <id>))>,<collect-om(?PreEBlock(<id>, _), conc); concat>)

  collect-split-om-post-eblock =
    !(<alltd(?PostEBlock(<id>, _))>, <collect-om(?PostEBlock(_, <id>), conc); concat>)

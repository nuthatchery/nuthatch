/**
 * Parses a Java source file to an abstract syntax tree.
 *
 * @author  Martin Bravenboer <martin@cs.uu.nl>
 */
module parse-java
imports liblib strategoxt-xtc-tools tool-doc jtree-version

strategies

  main-parse-java =
    xtc-io-wrap(
      parse-java-options
    , parse-java-usage
    , parse-java-about
    , !["sglr", "implode-asfix", "Java-15.tbl"]
    , parse-java
    )

  parse-java-options =
    release-option(|"Parse source")
    + symbol-option
    + preserve-comments-option
    + preserve-positions-option

strategies

  parse-java =
    xtc-sglr-no-heuristics(get-parse-table, get-start-symbol)

    ; if must-preserve-comments then
        xtc-transform(!"asfix-anno-comments")
      end

    ; if must-preserve-positions then
        xtc-transform(!"addPosInfo", !["-p", "Foo.java"])
      end

    ; xtc-implode-asfix

  xtc-sglr-no-heuristics(tbl, sort) =
    xtc-transform(!"sglr", !["-fi", "-fe", "-2A", "-p", <tbl; xtc-find> (), "-s", <sort> () | <pass-v-verbose> ()])

strategies

  symbol-option =
    ArgOption("-s" + "--start-symbol"
    , set-start-symbol
    , !HelpString("-s|--start-symbol s", "Start parsing with symbol s [CompilationUnit]")
    )

  set-start-symbol =
    <set-config> ("start-symbol", <id>)

  get-start-symbol = 
    <get-config> "start-symbol" <+ !"CompilationUnit"

strategies

  preserve-comments-option =
    Option("--preserve-comments"
    , <set-preserve-comments> "yes"
    , !HelpString("--preserve-comments", "Preserve source code
            comments as annotations of the abstract syntax tree. [off]")
    )

  set-preserve-comments =
    <set-config> ("preserve-comments", <id>)

  must-preserve-comments =
    <get-config> "preserve-comments" => "yes"

strategies

  preserve-positions-option =
    Option("--preserve-positions"
    , <set-preserve-positions> "yes"
    , !HelpString("--preserve-positions", "Preserve source code positions in the
                   input file as annotations of the abstract syntax tree. [off]")
    )

  set-preserve-positions =
    <set-config> ("preserve-positions", <id>)

  must-preserve-positions =
    <get-config> "preserve-positions" => "yes"

/**
 * Documentation
 */
strategies

  parse-java-usage =
    system-usage
/*
    <tool-doc>
      [ Usage("parse-java [OPTIONS]")
      , Summary(
         "Parses a Java source file to an abstract syntax 
          tree in the ATerm format.")
      , OptionUsage()
      , AutoReportBugs()
      ]
*/

  parse-java-about =
    system-about

/*
    <tool-doc>
      [ AutoProgram()
      , Author(Person("Martin Bravenboer", "martin@cs.uu.nl"))
      , GNU_LGPL("2002-2006", "Martin Bravenboer <martin@cs.uu.nl>")
      , Config([
          DefaultXTCRepository()
        , CurrentXTCRepository()
        ])
      ]
*/